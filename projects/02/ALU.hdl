// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Pre-setting x
    Mux16(a=x, b=false, sel=zx, out=zxOut); // If sel == 1, choose b
    Not16(in=zxOut, out=nxOut); // Negate zx
    Mux16(a=zxOut, b=nxOut, sel=nx, out=presetx); // If sel == 1, choose b

    // Pre-setting y
    Mux16(a=y, b=false, sel=zy, out=zyOut); // If sel == 1, choose b
    Not16(in=zyOut, out=nyOut); // Negate zy
    Mux16(a=zyOut, b=nyOut, sel=ny, out=presety); // If sel == 1, choose b

    // Selecting between computing & or +
    And16(a=presetx, b=presety, out=xAndy); // &
    Add16(a=presetx, b=presety, out=xSumy); // +
    Mux16(a=xAndy, b=xSumy, sel=f, out=compute); // If sel == 1, choose b

    // Post-setting the output
    Not16(in=compute, out=notCompute); // !out
    Mux16(a=compute, b=notCompute, sel=no, out=out, out[0..7]=low8, out[8..15]=high8, out[15]=selng); // If sel == 1, choose b
    
    // Setting zr - collapsing into 1 bit for mux
    Or8Way(in=low8, out=lowOut);
    Or8Way(in=high8, out=highOut);
    Or(a=lowOut, b=highOut, out=selzr);
    Mux(a=true, b=false, sel=selzr, out=zr); // If sel == 0, choose a

    // Setting ng (negative). If the most significant bit of the output is 1, then it's negative (according to 2's complement).
    Mux(a=false, b=true,sel=selng,out=ng); // If sel == 1 (negative according to 2's complement), choose b
}
