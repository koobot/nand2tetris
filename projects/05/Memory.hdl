// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    
    /* Number of registers = 2^15 = 32,768
    32,768 / 2 = 16,384 = when data memory ends
    So data memory first bit of address = 0
    Screen memory and kbd, first bit of address = 1
    */
    // Set load bits to choose between data memory or screen
    DMux(in=load, sel=address[14], a=dmLoad, b=sLoad);

    // Data memory: address 0 to 16,383 (bin: 0000 0000 0000 000 to 0111 1111 1111 111)
    // address is 14 bits
    RAM16K(in=in, load=dmLoad, address=address[0..13], out=dmOut);

    // Screen memory: address 16,384 to 24,575 (bin: 1000 0000 0000 000 to 1011 1111 1111 111)
    // address is 13 bits
    Screen(in=in, load=sLoad, address=address[0..12], out=sOut);

    // Keyboard memory: address 24,576 (bin: 1100 0000 0000 000)
    Keyboard(out=kOut);

    // Choose between outputs
    Mux4Way16(a=dmOut, b=dmOut, c=sOut, d=kOut, sel=address[13..14], out=out);
}
